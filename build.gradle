import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'base'
    id 'com.palantir.docker' version '0.25.0'
    id 'com.palantir.docker-run' version '0.25.0'
    id 'pl.allegro.tech.build.axion-release' version '1.10.1'
}

project.version = scmVersion.version

docker {
    name "${project.name}:${project.version}"
    files "plugins.txt", "seedJob.xml"
}

String dockerSockGroupId
Process process
if (DefaultNativePlatform.getCurrentOperatingSystem().isWindows()) {
    process = "docker run --rm -v /var/run/docker.sock:/var/run/docker.sock alpine stat -c %g /var/run/docker.sock".execute()
    def out = new ByteArrayOutputStream()
    process.waitForProcessOutput(out, System.err)
    dockerSockGroupId = out.toString().trim()
} else if (DefaultNativePlatform.getCurrentOperatingSystem().isLinux()) {
    process = "stat -c %g /var/run/docker.sock".execute()
    def out = new ByteArrayOutputStream()
    process.waitForProcessOutput(out, System.err)
    dockerSockGroupId = out.toString().trim()
} else {
    dockerSockGroupId = 'daemon'
}

dockerRun {
    name "${project.name}"
    image "${project.name}:${project.version}"
    ports '8082:8080'
    clean true
    daemonize false
    arguments '-v', '//var/run/docker.sock:/var/run/docker.sock', '--group-add', dockerSockGroupId
}